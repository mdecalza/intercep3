/*****************************************************************************
 * DECOHack Patch File
 * Projectiles
 * 2024 Matthew DeCalzadilla
 ****************************************************************************/

#include <dsdhacked>

#include "particles.dh"

/* Doom II projectiles */

// Rocket in flight
thing 34
{
	clear state spawn
	states
	{
		spawn:
			MISL A 5 bright a_spawnobject(259,0,-8,0,6,8)
			loop
	}
}

// BFG projectile
thing 36
{
	damage 125

	// MBF21 Properties (MBF21 feature set and later)
	splashgroup 1
}

// BFG impact
thing 43
{
	splashgroup 1
}

// BFG ball gives splash damage
state free 115 to 116
state fill 115
{
	BFS1 AB 4 Bright A_RadiusDamage(32, 512)
	Loop
}

// BFG collision gives splash damage
state 117
{
	BFE1 A 8 Bright A_RadiusDamage(128, 512)
}

// BFG tracers give splash damage
state 123
{
	BFE2 A 8 Bright A_RadiusDamage(48, 128)
}

// Increase rocket explosion damage
state 127
{
	MISL B 8 Bright A_RadiusDamage(192, 128)
}

/* New projectiles */

thing 151 "Incendiary projectile"
{
	health 1000
	speed 25
	radius 13
	height 8
	damage 3
	painchance 0

	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+MISSILE
	+TRANSLUCENT

	// MBF21 Properties (MBF21 feature set and later)
	splashgroup 1

	deathsound "STNMOV"

	States
	{
		Spawn:
			SP20 A 5 Bright {
				A_PlaySound("FIRSHT")
				A_RadiusDamage(80, 56)
			}
			SP20 B 5 Bright A_RadiusDamage(72, 56)
			SP20 C 5 Bright A_RadiusDamage(64, 56)
			SP20 D 5 Bright A_RadiusDamage(56, 56)
			Stop
		Death:
			BAL1 C 8 Bright {
				A_RadiusDamage(96, 64)
				A_Scream
			}
			BAL1 D 6 Bright
			BAL1 E 4 Bright
			Stop
	}
}

thing 153 "Arrow"
{
	health 20
	speed 25
	radius 13
	height 8
	mass 250
	damage 3
	painchance 0

	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+MISSILE

	deathsound "FRE031"

	States
	{
		Spawn:
			SP00 A 4
			Loop
		Death:
			SP00 B 4 A_Scream
			SP00 CDE 4
			Stop
	}
}