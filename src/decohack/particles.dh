/*****************************************************************************
 * DECOHack Main Patch
 * Particle
 * 2025 Lina, Matthew DeCalzadilla
 *****************************************************************************/

#include <dsdhacked>

// Plasma impact spawns yellow arcs
state 109
{
	PLSE A 6 Bright A_SpawnObject(260)
}

// Rocket in flight spawns trail
state 114
{
	MISL A 5 Bright A_SpawnObject(259, 0.0, -8.0, 0.0, 6.0, 8.0)
}

// BFG ball spawns particles
state 116
{
	BFS1 B 4 Bright A_SpawnObject(263)
}

// BFG impact spawns particles
state 118
{
	BFE1 B 8 Bright A_SpawnObject(263)
}

state 120
{
	BFE1 D 8 Bright A_SpawnObject(263)
}

// Teleport fog spawns particles
state 131
{
	TFOG B 6 Bright A_SpawnObject(263)
}

// Baron/hell knight fireball spawns nukage droplet
state 522
{
	BAL7 A 4 Bright A_SpawnObject(258, 0.0, -8.0, 0.0, 0.0, 0.0, 0.0, 0.25)
}

// Lost soul spawns embers on charge
state 591
{
	SKUL C 4 Bright A_SpawnObject(250, 0.0, -8.0, 0.0, 15.0, 0.0, 0.0, 0.5)
}

// Arachnotron plasma impact spawns green arcs
state 669
{
	APBX A 5 Bright A_SpawnObject(261)
}

// Custom particle spawners

thing 249 "Embers"
{
	//$category VFX
	ednum 200
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+NOGRAVITY

	States
	{
		Spawn: 
			TNT1 A 69
			TNT1 A 1 A_RandomJump(RisePick, 50)
			TNT1 A 1 A_RandomJump(RisePick, 63)
			TNT1 A 1 A_RandomJump(RisePick, 85)
			TNT1 A 1 A_RandomJump(RisePick, 127)
			Goto RisePick
		RisePick:
			TNT1 A 1
			TNT1 A 0 A_RandomJump(Rise2, 27)
			TNT1 A 0 A_RandomJump(Rise3, 31)
			TNT1 A 0 A_RandomJump(Rise4, 36)
			TNT1 A 0 A_RandomJump(Rise5, 42)
			TNT1 A 0 A_RandomJump(Rise6, 50)
			TNT1 A 0 A_RandomJump(Rise7, 63)
			TNT1 A 0 A_RandomJump(Rise8, 85)
			TNT1 A 0 A_RandomJump(Rise9, 127)
			Goto Rise1
		Rise1:
			TNT1 A 1 A_SpawnObject(250, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.5)
			Goto Spawn
		Rise2:
			TNT1 A 1 A_SpawnObject(250, 0.0, 42.0, 0.0, 0.0, 0.0, 0.0, 0.5)
			Goto Spawn
		Rise3:
			TNT1 A 1 A_SpawnObject(250, 0.0, -42.0, 0.0, 0.0, 0.0, 0.0, 0.5)
			Goto Spawn
		Rise4:
			TNT1 A 1 A_SpawnObject(250, 0.0, 0.0, 42.0, 0.0, 0.0, 0.0, 0.5)
			Goto Spawn
		Rise5:
			TNT1 A 1 A_SpawnObject(250, 0.0, 0.0,-42.0, 0.0, 0.0, 0.0, 0.5)
			Goto Spawn
		Rise6:
			TNT1 A 1 A_SpawnObject(250, 0.0, 42.0, 42.0, 0.0, 0.0, 0.0, 0.5)
			Goto Spawn
		Rise7:
			TNT1 A 1 A_SpawnObject(250, 0.0, -42.0, 42.0, 0.0, 0.0, 0.0, 0.5)
			Goto Spawn
		Rise8:
			TNT1 A 1 A_SpawnObject(250, 0.0, 42.0, -42.0, 0.0, 0.0, 0.0, 0.5)
			Goto Spawn
		Rise9:
			TNT1 A 1 A_SpawnObject(250, 0.0, -42.0, -42.0, 0.0, 0.0, 0.0, 0.5)
			Goto Spawn
	}
}

thing 250 "Ember"
{
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+TRANSLUCENT

	States
	{
		Spawn:
			TNT1 A 1
			Goto Pick
		Pick:
			TNT1 A 0 {
			A_RandomJump(Type2, 85)
			A_RandomJump(Type3, 127)
			}
			Goto Type1
		Type1:
			SP98 EDCB 2 Bright
			SP98 A 10 Bright A_RemoveFlags(TRANSLUCENT)
			SP98 BC 8 Bright
			SP98 C 6 Bright A_AddFlags(TRANSLUCENT)
			SP98 DE 6 Bright
			Goto XDeath
		Type2:
			SP98 JIHG 2 Bright
			SP98 F 13 Bright A_RemoveFlags(TRANSLUCENT)
			SP98 GH 11 Bright
			SP98 H 9 Bright A_AddFlags(TRANSLUCENT)
			SP98 IJ 9 Bright
			Goto XDeath
		Type3:
			SP98 ONML 2 Bright
			SP98 K 16 Bright A_RemoveFlags(TRANSLUCENT)
			SP98 LM 14 Bright
			SP98 M 12 Bright A_AddFlags(TRANSLUCENT)
			SP98 NO 12 Bright
			Goto XDeath
		Death:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 1
			Stop
	}
}

thing 251 "Rain"
{
	//$category VFX
	ednum 201
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+SPAWNCEILING
	+NOGRAVITY

	States
	{
		Spawn:
			TNT1 A 35 A_Look
			Loop
		See:
			TNT1 A 35 A_JumpIfTargetCloser(RandomDelay, 2048.0)
			TNT1 A 35 A_RandomJump(FallPick, 72)
			Loop
		RandomDelay:
			TNT1 A 5 A_RandomJump(FallPick, 50)
			TNT1 A 3 A_RandomJump(FallPick, 63)
			TNT1 A 4 A_RandomJump(FallPick, 85)
			TNT1 A 10 A_RandomJump(FallPick, 127)
			Goto See
		FallPick:
			TNT1 A 1
			TNT1 A 0 A_RandomJump(Fall2, 27)
			TNT1 A 0 A_RandomJump(Fall3, 31)
			TNT1 A 0 A_RandomJump(Fall4, 36)
			TNT1 A 0 A_RandomJump(Fall5, 42)
			TNT1 A 0 A_RandomJump(Fall6, 50)
			TNT1 A 0 A_RandomJump(Fall7, 63)
			TNT1 A 0 A_RandomJump(Fall8, 85)
			TNT1 A 0 A_RandomJump(Fall9, 127)
			Goto Fall1
		Fall1:
			TNT1 A 1 A_SpawnObject(252, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, -16.0)
			Goto See
		Fall2:
			TNT1 A 1 A_SpawnObject(252, 0.0, 42.0,0.0,0.0,0.0,0.0,-16.0)
			Goto See
		Fall3:
			TNT1 A 1 A_SpawnObject(252, 0.0, -42.0, 0.0, 0.0, 0.0, 0.0, -16.0)
			Goto See
		Fall4:
			TNT1 A 1 A_SpawnObject(252, 0.0, 0.0, 42.0, 0.0, 0.0, 0.0, -16.0)
			Goto See
		Fall5:
			TNT1 A 1 A_SpawnObject(252, 0.0, 0.0, -42.0, 0.0, 0.0, 0.0, -16.0)
			Goto See
		Fall6:
			TNT1 A 1 A_SpawnObject(252, 0.0, 42.0, 42.0, 0.0, 0.0, 0.0, -16.0)
			Goto See
		Fall7:
			TNT1 A 1 A_SpawnObject(252, 0.0, -42.0, 42.0, 0.0, 0.0, 0.0, -16.0)
			Goto See
		Fall8:
			TNT1 A 1 A_SpawnObject(252, 0.0, 42.0, -42.0, 0.0, 0.0, 0.0, -16.0)
			Goto See
		Fall9:
			TNT1 A 1 A_SpawnObject(252, 0.0, -42.0, -42.0, 0.0, 0.0, 0.0, -16.0)
			Goto See
	}
}

thing 252 "Raindrop"
{
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+MISSILE
	+NOBLOCKMAP

	States
	{
		Spawn:
			SP99 A 8
			Loop
		Death:
			SP99 BCDEFGH 1
			Stop
	}
}

thing 253 "Ash"
{
	//$category VFX
	ednum 202
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+NOGRAVITY

	States
	{
		Spawn: 
			TNT1 A 69
			TNT1 A 1 A_RandomJump(RisePick, 50)
			TNT1 A 1 A_RandomJump(RisePick, 63)
			TNT1 A 1 A_RandomJump(RisePick, 85)
			TNT1 A 1 A_RandomJump(RisePick, 127)
			Goto RisePick
		RisePick:
			TNT1 A 1
			TNT1 A 0 A_RandomJump(Rise2, 27)
			TNT1 A 0 A_RandomJump(Rise3, 31)
			TNT1 A 0 A_RandomJump(Rise4, 36)
			TNT1 A 0 A_RandomJump(Rise5, 42)
			TNT1 A 0 A_RandomJump(Rise6, 50)
			TNT1 A 0 A_RandomJump(Rise7, 63)
			TNT1 A 0 A_RandomJump(Rise8, 85)
			TNT1 A 0 A_RandomJump(Rise9, 127)
			Goto Rise1
		Rise1:
			TNT1 A 1 A_SpawnObject(254, 0.0, 0.0, 0.0, 0.0, 1.0, 0.0, 1.0)
			Goto Spawn
		Rise2:
			TNT1 A 1 A_SpawnObject(254, 0.0, 42.0, 0.0, 0.0, 1.0, 0.0, 1.0)
			Goto Spawn
		Rise3:
			TNT1 A 1 A_SpawnObject(254, 0.0, -42.0, 0.0, 0.0, 1.0, 0.0, 1.0)
			Goto Spawn
		Rise4:
			TNT1 A 1 A_SpawnObject(254, 0.0, 0.0, 42.0, 0.0, 1.0, 0.0, 1.0)
			Goto Spawn
		Rise5:
			TNT1 A 1 A_SpawnObject(254, 0.0, 0.0, -42.0, 0.0, 1.0, 0.0, 1.0)
			Goto Spawn
		Rise6:
			TNT1 A 1 A_SpawnObject(254, 0.0, 42.0, 42.0, 0.0, 1.0, 0.0, 1.0)
			Goto Spawn
		Rise7:
			TNT1 A 1 A_SpawnObject(254, 0.0, -42.0, 42.0, 0.0, 1.0, 0.0, 1.0)
			Goto Spawn
		Rise8:
			TNT1 A 1 A_SpawnObject(254, 0.0, 42.0, -42.0, 0.0, 1.0, 0.0, 1.0)
			Goto Spawn
		Rise9:
			TNT1 A 1 A_SpawnObject(254, 0.0, -42.0, -42.0, 0.0, 1.0, 0.0, 1.0)
			Goto Spawn
	}
}

thing 254 "Ash particle"
{
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+NOGRAVITY
	+NOBLOCKMAP
	+BOUNCES
	+TRANSLUCENT

	States
	{
		Spawn:
			TNT1 A 1
			Goto Pick
		Pick:
			TNT1 A 0 {
			A_RandomJump(Type2, 85)
			A_RandomJump(Type3, 127)
			}
			Goto Type1
		Type1:
			SPA0 EDCB 2 Bright
			SPA0 A 10 Bright A_RemoveFlags(TRANSLUCENT)
			SPA0 BC 8 Bright
			SPA0 C 6 Bright A_AddFlags(TRANSLUCENT)
			SPA0 DE 6 Bright
			Goto XDeath
		Type2:
			SPA0 JIHG 2 Bright
			SPA0 F 13 Bright A_RemoveFlags(TRANSLUCENT)
			SPA0 GH 11 Bright
			SPA0 H 9 Bright A_AddFlags(TRANSLUCENT)
			SPA0 IJ 9 Bright
			Goto XDeath
		Type3:
			SPA0 ONML 2 Bright
			SPA0 K 16 Bright A_RemoveFlags(TRANSLUCENT)
			SPA0 LM 14 Bright
			SPA0 M 12 Bright A_AddFlags(TRANSLUCENT)
			SPA0 NO 12 Bright
			Goto XDeath
		Death:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 1
			Stop
	}
}

thing 255 "Blood drip"
{
	//$category VFX
	ednum 203
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+SPAWNCEILING
	+NOGRAVITY

	States
	{
		Spawn: 
			TNT1 A 34
			TNT1 A 1 A_RandomJump(FallPick, 50)
			TNT1 A 1 A_RandomJump(FallPick, 63)
			TNT1 A 1 A_RandomJump(FallPick, 85)
			TNT1 A 1 A_RandomJump(FallPick, 127)
			Goto FallPick
		FallPick:
			TNT1 A 1
			TNT1 A 0 A_RandomJump(Fall2, 27)
			TNT1 A 0 A_RandomJump(Fall3, 31)
			TNT1 A 0 A_RandomJump(Fall4, 36)
			TNT1 A 0 A_RandomJump(Fall5, 42)
			TNT1 A 0 A_RandomJump(Fall6, 50)
			TNT1 A 0 A_RandomJump(Fall7, 63)
			TNT1 A 0 A_RandomJump(Fall8, 85)
			TNT1 A 0 A_RandomJump(Fall9, 127)
			Goto Fall1
		Fall1:
			TNT1 A 1 A_SpawnObject(256, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, -16.0)
			Goto Spawn
		Fall2:
			TNT1 A 1 A_SpawnObject(256, 0.0, 42.0, 0.0, 0.0, 0.0, 0.0, -16.0)
			Goto Spawn
		Fall3:
			TNT1 A 1 A_SpawnObject(256, 0.0, -42.0, 0.0, 0.0, 0.0, 0.0, -16.0)
			Goto Spawn
		Fall4:
			TNT1 A 1 A_SpawnObject(256, 0.0, 0.0, 42.0, 0.0, 0.0, 0.0, -16.0)
			Goto Spawn
		Fall5:
			TNT1 A 1 A_SpawnObject(256, 0.0, 0.0, -42.0, 0.0, 0.0, 0.0, -16.0)
			Goto Spawn
		Fall6:
			TNT1 A 1 A_SpawnObject(256, 0.0, 42.0, 42.0, 0.0, 0.0, 0.0, -16.0)
			Goto Spawn
		Fall7:
			TNT1 A 1 A_SpawnObject(256, 0.0, -42.0, 42.0, 0.0, 0.0, 0.0, -16.0)
			Goto Spawn
		Fall8:
			TNT1 A 1 A_SpawnObject(256, 0.0, 42.0, -42.0, 0.0, 0.0, 0.0, -16.0)
			Goto Spawn
		Fall9:
			TNT1 A 1 A_SpawnObject(256, 0.0, -42.0, -42.0, 0.0, 0.0, 0.0, -16.0)
			Goto Spawn
	}
}

thing 256 "Blood droplet"
{
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+MISSILE
	+NOBLOCKMAP

	States
	{
		Spawn:
			SPA1 A 8
			Loop
		Death:
			SPA1 BCDEFGH 1
			Stop
	}
}

thing 257 "Nukage burble"
{
	//$category VFX
	ednum 204
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+NOGRAVITY

	States
	{
		Spawn: 
			TNT1 A 105
			TNT1 A 7 A_RandomJump(RisePick, 50)
			TNT1 A 7 A_RandomJump(RisePick, 63)
			TNT1 A 7 A_RandomJump(RisePick, 85)
			TNT1 A 7 A_RandomJump(RisePick, 127)
			Goto RisePick
		RisePick:
			TNT1 A 1
			TNT1 A 0 A_RandomJump(Rise2, 27)
			TNT1 A 0 A_RandomJump(Rise3, 31)
			TNT1 A 0 A_RandomJump(Rise4, 36)
			TNT1 A 0 A_RandomJump(Rise5, 42)
			TNT1 A 0 A_RandomJump(Rise6, 50)
			TNT1 A 0 A_RandomJump(Rise7, 63)
			TNT1 A 0 A_RandomJump(Rise8, 85)
			TNT1 A 0 A_RandomJump(Rise9, 127)
			Goto Rise1
		Rise1:
			TNT1 A 1 A_SpawnObject(258, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 2.5)
			Goto Spawn
		Rise2:
			TNT1 A 1 A_SpawnObject(258, 0.0, 42.0, 0.0, 0.0, 0.0, 0.0, 2.5)
			Goto Spawn
		Rise3:
			TNT1 A 1 A_SpawnObject(258, 0.0, -42.0, 0.0, 0.0, 0.0, 0.0, 2.5)
			Goto Spawn
		Rise4:
			TNT1 A 1 A_SpawnObject(258, 0.0, 0.0, 42.0, 0.0, 0.0, 0.0, 2.5)
			Goto Spawn
		Rise5:
			TNT1 A 1 A_SpawnObject(258, 0.0, 0.0, -42.0, 0.0, 0.0, 0.0, 2.5)
			Goto Spawn
		Rise6:
			TNT1 A 1 A_SpawnObject(258, 0.0, 42.0, 42.0, 0.0, 0.0, 0.0, 2.5)
			Goto Spawn
		Rise7:
			TNT1 A 1 A_SpawnObject(258, 0.0, -42.0, 42.0, 0.0, 0.0, 0.0, 2.5)
			Goto Spawn
		Rise8:
			TNT1 A 1 A_SpawnObject(258, 0.0, 42.0, -42.0, 0.0, 0.0, 0.0, 2.5)
			Goto Spawn
		Rise9:
			TNT1 A 1 A_SpawnObject(258, 0.0, -42.0, -42.0, 0.0, 0.0, 0.0, 2.5)
			Goto Spawn
	}
}

thing 258 "Nukage droplet"
{
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+LOGRAV

	States
	{
		Spawn:
			TNT1 A 1
			Goto Pick
		Pick:
			TNT1 A 0 {
			A_RandomJump(Type2, 85)
			A_RandomJump(Type3, 127)
			}
			Goto Type1
		Type1:
			SPA2 A 8
			SPA2 BC 6
			SPA2 C 5 A_AddFlags(TRANSLUCENT)
			SPA2 DE 5
			Goto XDeath
		Type2:
			SPA2 F 8
			SPA2 GH 6
			SPA2 H 5 A_AddFlags(TRANSLUCENT)
			SPA2 IJ 5
			Goto XDeath
		Type3:
			SPA2 K 8
			SPA2 LM 6
			SPA2 M 5 A_AddFlags(TRANSLUCENT)
			SPA2 NO 5
			Goto XDeath
		Death:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 1
			Stop
	}
}

thing 259 "Rocket trail"
{
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+NOGRAVITY
	+NOBLOCKMAP
	+TRANSLUCENT

	States
	{
		Spawn:
			TNT1 A 2
			SPA3 A 2 Bright
			SPA3 B 2 Bright {
			A_RandomJump(long1, 85)
			A_RandomJump(long2, 127)
			}
			SPA3 CDEF 2 Bright
			Stop
		long1:
			SPA3 CDEF 3 Bright
			Stop
		long2:
			SPA3 CDEF 4 Bright
			Stop
	}
}

thing 260 "Yellow plasma arc"
{
	health 1000
	speed 20
	radius 1
	height 1
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	splashgroup 1  // Don't damage player

	+NOGRAVITY
	+NOBLOCKMAP
	+TRANSLUCENT

	States
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 Bright A_RandomJump(Spawn2, 15)
			TNT1 A 0 Bright A_RandomJump(Spawn3, 15)
			TNT1 A 0 Bright A_RandomJump(Spawn4, 15)
			TNT1 A 0 Bright A_RandomJump(Spawn5, 15)
			TNT1 A 0 Bright A_RandomJump(Spawn6, 15)
			TNT1 A 0 Bright A_RandomJump(Spawn7, 15)
			TNT1 A 0 Bright A_RandomJump(Spawn8, 15)
			TNT1 A 0 Bright A_RandomJump(Spawn9, 15)
			Stop
		Spawn2:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(260, 0.0, 30.0, -30.0, 0.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn3:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(260, 0.0, -30.0, -30.0, 0.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn4:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(260, 0.0, 0.0, 30.0, 30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn5:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(260, 0.0, 0.0, 30.0, -30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn6:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(260, 0.0, 30.0, 0.0, 30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn7:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(260, 0.0, -30.0, 0.0, -30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn8:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(260, 0.0, 0.0, 30.0, -30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn9:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(260, 0.0, 0.0, 30.0, -30.0, 0.0, 0.0, 0.0)
			Goto Die
		Die:
			PLSE EDE 5 Bright
			Stop
	}
}

thing 261 "Green plasma arc"
{
	health 1000
	speed 20
	radius 1
	height 1
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+NOGRAVITY
	+NOBLOCKMAP
	+TRANSLUCENT

	States
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 Bright A_RandomJump(Spawn2, 12)
			TNT1 A 0 Bright A_RandomJump(Spawn3, 12)
			TNT1 A 0 Bright A_RandomJump(Spawn4, 12)
			TNT1 A 0 Bright A_RandomJump(Spawn5, 12)
			TNT1 A 0 Bright A_RandomJump(Spawn6, 12)
			TNT1 A 0 Bright A_RandomJump(Spawn7, 12)
			TNT1 A 0 Bright A_RandomJump(Spawn8, 12)
			TNT1 A 0 Bright A_RandomJump(Spawn9, 12)
			Stop
		Spawn2:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(261, 0.0, 30.0, -30.0, 0.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn3:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(261, 0.0, -30.0, -30.0, 0.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn4:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(261, 0.0, 0.0, 30.0, 30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn5:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(261, 0.0, 0.0, 30.0, -30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn6:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(261, 0.0, 30.0, 0.0, 30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn7:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(261, 0.0, -30.0, 0.0, -30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn8:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(261, 0.0, 0.0, 30.0, -30.0, 0.0, 0.0, 0.0)
			Goto Die
		Spawn9:
			TNT1 A 1
			TNT1 A 0 Bright A_SpawnObject(261, 0.0, 0.0, 30.0, -30.0, 0.0, 0.0, 0.0)
			Goto Die
		Die:
			APBX EDCDE 5 Bright
			Stop
	}
}

thing 262 "BFG particle 1"
{
	health 1000
	speed 20
	radius 1
	height 1
	damage 0
	reactiontime 8
	painchance 0
	mass 10

	+NOGRAVITY
	+NOBLOCKMAP
	+TRANSLUCENT
	+NOCLIP

	States
	{
		Spawn:
			TFOG GHIJIHG 5 Bright
			Stop
	}
}

thing 263 "BFG particle 2"
{
	health 1000
	speed 20
	radius 1
	height 1
	damage 0
	reactiontime 8
	painchance 0
	mass 10

	+NOGRAVITY
	+NOBLOCKMAP
	+TRANSLUCENT
	+NOCLIP

	States
	{
		Spawn:
			TNT1 A 1
			TNT1 A 1 {
				A_RandomJump(Die, 30)
				A_SpawnObject(262, 0.0, 30.0, 30.0, -5.0, -1.0, 0.0, 0.0)
				A_Turn(60)
			}
			TNT1 A 1 {
				A_RandomJump(Die, 60)
				A_SpawnObject(262, 0.0, -30.0, -30.0, -5.0, -1.0, -1.0, 0.5)
				A_Turn(60)
			}
			TNT1 A 1 {
				A_RandomJump(Die, 90)
				A_SpawnObject(262, 0.0, 0.0, -30.0, 30.0, -1.0, 0.0, 0.0)
				A_Turn(60)
			}
			TNT1 A 1 {
				A_RandomJump(Die, 120)
				A_SpawnObject(262, 0.0, 0.0, 30.0, -30.0, -0.5, -1.0, 0.5)
				A_Turn(60)
			}
			TNT1 A 1 {
				A_RandomJump(Die, 150)
				A_SpawnObject(262, 0.0, 30.0, 0.0, 30.0, 1.0, -1.0, 0.0)
				A_Turn(60)
			}
			TNT1 A 1 {
				A_RandomJump(Die, 180)
				A_SpawnObject(262, 0.0, -30.0, 0.0, -30.0, 0.0, 1.0, 1.0)
				A_Turn(60)
			}
			TNT1 A 1 {
				A_RandomJump(Die, 210)
				A_SpawnObject(262, 0.0, 0.0, 30.0, -30.0, 1.0, -1.0, 0.0)
				A_Turn(60)
			}
			TNT1 A 1 {
				A_RandomJump(Die, 240)
				A_SpawnObject(262, 0.0, 0.0, -30.0, 30.0, -1.0, 1.0, 0.5)
				A_Turn(60)
			}
			Goto Die
		Die:
			TNT1 A 1
			Stop
	}
}

thing 264 "Air bubbles"
{
	//$category VFX
	ednum 205
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+NOGRAVITY

	States
	{
		Spawn: 
			TNT1 A 30
			TNT1 A 1 A_RandomJump(Rise, 25)
			Loop
		Rise:
			TNT1 A 1 A_SpawnObject(265, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 1.0)
			Goto Spawn
	}
}

thing 265 "Air bubble"
{
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+NOGRAVITY
	+NOBLOCKMAP
	+TRANSLUCENT

	States
	{
		Spawn:
			TNT1 A 1 A_RandomJump(Type2, 42)
			Goto Type1
		Type1:
			BUBB A 140
			Goto XDeath
		Type2:
			GBUB A 105
			Goto XDeath
		Death:
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 1
			Stop
	}
}

thing 266 "Snow"
{
	//$category VFX
	ednum 206
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 100

	+SPAWNCEILING
	+NOGRAVITY

	States
	{
		Spawn: 
			TNT1 A 35 A_Look
			Loop
		See:
			TNT1 A 35 A_JumpIfTargetCloser(RandomDelay, 2048.0)
			TNT1 A 35 A_RandomJump(FallPick, 56)
			Loop
		RandomDelay:
			TNT1 A 5 A_RandomJump(FallPick, 50)
			TNT1 A 3 A_RandomJump(FallPick, 63)
			TNT1 A 4 A_RandomJump(FallPick, 85)
			TNT1 A 16 A_RandomJump(FallPick, 127)
			Goto See
		FallPick:
			TNT1 A 1
			TNT1 A 0 A_RandomJump(Fall2, 27)
			TNT1 A 0 A_RandomJump(Fall3, 31)
			TNT1 A 0 A_RandomJump(Fall4, 36)
			TNT1 A 0 A_RandomJump(Fall5, 42)
			TNT1 A 0 A_RandomJump(Fall6, 50)
			TNT1 A 0 A_RandomJump(Fall7, 63)
			TNT1 A 0 A_RandomJump(Fall8, 85)
			TNT1 A 0 A_RandomJump(Fall9, 127)
			Goto Fall1
		Fall1:
			TNT1 A 1 A_SpawnObject(267, 0.0, 0.0, 0.0, 0.0, 1.0, 0.0, -4.0)
			Goto See
		Fall2:
			TNT1 A 1 A_SpawnObject(267, 0.0, 42.0, 0.0, 0.0, -1.0, 0.0, -4.0)
			Goto See
		Fall3:
			TNT1 A 1 A_SpawnObject(267, 0.0, -42.0, 0.0, 0.0, 0.0, 1.0, -4.0)
			Goto See
		Fall4:
			TNT1 A 1 A_SpawnObject(267, 0.0, 0.0, 42.0, 0.0, 0.0, -1.0, -4.0)
			Goto See
		Fall5:
			TNT1 A 1 A_SpawnObject(267, 0.0, 0.0, -42.0, 0.0, 1.0, 1.0, -4.0)
			Goto See
		Fall6:
			TNT1 A 1 A_SpawnObject(267, 0.0, 42.0, 42.0, 0.0, 1.0, -1.0, -4.0)
			Goto See
		Fall7:
			TNT1 A 1 A_SpawnObject(267, 0.0, -42.0, 42.0, 0.0, -1.0, 1.0, -4.0)
			Goto See
		Fall8:
			TNT1 A 1 A_SpawnObject(267, 0.0, 42.0, -42.0, 0.0, -1.0, -1.0, -4.0)
			Goto See
		Fall9:
			TNT1 A 1 A_SpawnObject(267, 0.0, -42.0, -42.0, 0.0, 0.0, 0.0, -4.0)
			Goto See
	}
}

thing 267 "Snowflake"
{
	health 1000
	speed 0
	radius 0
	height 0
	damage 0
	reactiontime 8
	painchance 0
	mass 1

	+NOGRAVITY
	+NOBLOCKMAP

	States
	{
		Spawn:
			TNT1 A 1 A_RandomJump(Type3, 63)
			TNT1 A 1 A_RandomJump(Type2, 127)
			Goto Type1
		Type1:
			SNOW A 70 A_RandomJump(Death, 63)
			Loop
		Type2:
			SNOW B 70 A_RandomJump(Death, 47)
			Loop
		Type3:
			SNOW C 70 A_RandomJump(Death, 31)
			Loop
		Death:
			TNT1 A 1
			Stop
	}
}