/*****************************************************************************
 * DECOHack Patch File
 * Enemies
 * 2024 Matthew DeCalzadilla
 ****************************************************************************/

#include <dsdhacked>

#include "projectiles.dh"

/* Doom II enemies */

thing 3 "Former Sergeant"
{
	dropitem 70  // Drops shells on death instead of a shotgun
}

thing 11 "Chaingunner"
{
	dropitem 64  // Drops bullets on death instead of a minigun
}

// Imp
thing 12
{
	projectilegroup 1  // Imp fireball projectile group
}

// Lost Soul
thing 19
{
	health 60
}

// Arachnotron
thing 21
{
	projectilegroup 2  // Arachnotron plasma projectile group
}

/* New enemies */

// Boss monster
thing 206 "Behemoth"
{
	radius 16
	height 16
}

// Final boss monster
thing 208 "Barbat-OS"
{
	radius 16
	height 16
}

// Fires a deadly incendiary round
thing 242 : thing 11 "Incendiary zombie"
{
	//$category Monsters/Fodder
	ednum 6091

	health 80

	// Extended Properties (Extended/DEHEXTRA feature set and later)
	dropitem 70  // Drops shells on death

	// MBF21 Properties (MBF21 feature set and later)
	meleerange 192
	splashgroup 1  // Immune to incendiary splash damage

	States
	{
		Spawn:
			SP91 AABB 5 A_Look
			Loop
		See:
			SP91 AABBCCDD 3 {
				A_Chase
				A_JumpIfTargetCloser(Missile, 192.0)
			}
			Loop
		Missile:
			SP91 E 8 A_FaceTarget
			SP91 E 0 A_RandomJump(Flip, 127)
			SP91 F 3 Bright {
				A_MonsterProjectile(151, -10.0)
				A_PlaySound("DSHTGN")
			}
			SP91 F 3 Bright A_MonsterProjectile(151, -6.0)
			SP91 F 3 Bright A_MonsterProjectile(151, -2.0)
			SP91 F 3 Bright A_MonsterProjectile(151, 2.0)
			SP91 F 3 Bright {
				A_MonsterProjectile(151, 6.0)
				A_PlaySound("FLAME")
			}
			SP91 F 3 Bright A_MonsterProjectile(151, 10.0)
			SP91 F 3 Bright A_MonsterProjectile(151, 6.0)
			SP91 F 3 Bright A_MonsterProjectile(151, 2.0)
			SP91 F 3 Bright A_MonsterProjectile(151, -2.0)
			SP91 F 0 Bright A_PlaySound("SGCOCK")
			SP91 C 12 A_FaceTarget
			Goto See
		Flip:
			SP91 F 3 Bright {
				A_MonsterProjectile(151, 10.0)
				A_PlaySound("DSHTGN")
			}
			SP91 F 3 Bright A_MonsterProjectile(151, 6.0)
			SP91 F 3 Bright A_MonsterProjectile(151, 2.0)
			SP91 F 3 Bright A_MonsterProjectile(151, -2.0)
			SP91 F 3 Bright {
				A_MonsterProjectile(151, -6.0)
				A_PlaySound("FLAME")
			}
			SP91 F 3 Bright A_MonsterProjectile(151, -10.0)
			SP91 F 3 Bright A_MonsterProjectile(151, -6.0)
			SP91 F 3 Bright A_MonsterProjectile(151, -2.0)
			SP91 F 3 Bright A_MonsterProjectile(151, 2.0)
			SP91 F 0 Bright A_PlaySound("SGCOCK")
			SP91 E 12 A_FaceTarget
			Goto See
		Pain:
			SP91 G 3
			SP91 G 3 A_Pain
			Goto See
		Death:
			SP91 H 5
			SP91 I 5 A_Scream
			SP91 J 5 A_Fall
			SP91 KLM 5
			SP91 N -1
			Stop
		XDeath:
			SP91 O 5
			SP91 P 5 A_XScream
			SP91 Q 5 A_Fall
			SP91 RS 5
			SP91 T -1
			Stop
		Raise:
			SP91 NMLKJIH 5
			Goto See
	}
}

// Fires a spread of 3 arachnotron plasma balls
thing 243 : thing 3 "Plasma zombie"
{
	//$category Monsters/Fodder
	ednum 6092

	health 40

	// Extended Properties (Extended/DEHEXTRA feature set and later)
	dropitem 68  // Drops a cell charge on death

	// MBF21 Properties (MBF21 feature set and later)
	projectilegroup 2  // Immune to arachnotron plasma damage

	States
	{
		Spawn:
			SP92 AABB 5 A_Look
			Loop
		See:
			SP92 AABBCCDD 3 A_Chase
			Loop
		Missile:
			SP92 EEE 8 A_FaceTarget
			SP92 F 10 Bright {
				A_MonsterProjectile(37, 6.0)
				A_MonsterProjectile(37)
				A_MonsterProjectile(37, -6.0)
				A_PlaySound("PLASMA")
			}
			SP92 E 10
			Goto See
		Pain:
			SP92 G 3
			SP92 G 3 A_Pain
			Goto See
		Death:
			SP92 H 5
			SP92 I 5 A_Scream
			SP92 J 5 A_Fall
			SP92 K 5
			SP92 L -1
			Stop
		XDeath:
			SP92 M 5
			SP92 N 5 A_XScream
			SP92 O 5 A_Fall
			SP92 PQRST 5
			SP92 U -1
			Stop
		Raise:
			SP92 KJIH 5
			Goto See
	}
}

// Glass cannon -- fires 1 rocket after long wind-up
thing 244 : thing 2 "Rocket zombie"
{
	//$category Monsters/Fodder
	ednum 6093

	health 10

	// MBF21 Flags (MBF21 feature set only)
	+NORADIUSDMG

	// Extended Properties (Extended/DEHEXTRA feature set and later)
    dropitem 66  // Drops a rocket on death

	// MBF21 Properties (MBF21 feature set and later)
	projectilegroup 1  // Immune to arachnotron plasma damage

	States
	{
		Spawn:
			SP93 AABB 5 A_Look
			Loop
		See:
			SP93 AABBCCDD 3 A_Chase
			Loop
		Missile:
			SP93 EE 5 Bright A_FaceTarget
			SP93 EE 5 A_FaceTarget
			SP93 EE 5 Bright A_FaceTarget
			SP93 EE 5 A_FaceTarget
			SP93 EE 5 Bright A_FaceTarget
			SP93 F 10 Bright A_CyberAttack
			SP93 E 10
			Goto See
		Pain:
			SP93 G 3
			SP93 G 3 A_Pain
			Goto See
		Death:
			SP93 H 5
			SP93 I 5 A_Scream
			SP93 J 5 A_Fall
			SP93 K 5
			SP93 L -1
			Stop
		XDeath:
			SP93 M 5
			SP93 N 5 A_XScream
			SP93 O 5 A_Fall
			SP93 PQRST 5
			SP93 U -1
			Stop
		Raise:
			SP93 KJIH 5
			Goto See
	}
}

// Weaker, smaller masterminds
thing 245 : thing 20 "Former tank"
{
	//$category Monsters/Mid-tier
	ednum 6094

	health 1000
	radius 24
	height 64
	painchance 10
	mass 1200

	+NOBLOOD

	// MBF21 Flags (MBF21 feature set only)
	+NORADIUSDMG
	-FULLVOLSOUNDS

	seesound "FRE027"
	painsound "FRE028"
	deathsound "FRE025"

	// Extended Properties (Extended/DEHEXTRA feature set and later)
	dropitem 65  // Drops a box of ammo on death

	States
	{
		Spawn:
			SP94 AABB 5 A_Look
			Loop
		See:
			SP94 AAB 3 A_Chase
			SP94 B 3 A_PlaySound("FRE024")
			Loop
		Missile:
			SP94 B 20 Bright A_FaceTarget
		MissileRefire:
			SP94 DE 4 Bright A_SPosAttack
			SP94 A 1 A_SpidRefire
			Goto MissileRefire
		Pain:
			SP94 F 3
			SP94 F 3 A_Pain
			Goto See
		Death:
			SP94 G 10 Bright
			SP94 K 10 Bright A_Scream
			SP94 L 8 Bright A_Fall
			SP94 M 6 Bright {
				A_SpawnObject(2, 0.0, 0.0, 0.0, 0.0, -2.0, 0.0, 0.0)
				A_Explode
			}
			SP94 N 4 Bright
			SP94 O -1
			Stop
	}
}

// thing 246 defined in hazards.dh

// Can jump up and down ledges
thing 247 : thing 12 "Gliding imp"
{
	//$category Monsters/Fodder
	ednum 6096

	mass 75

	+DROPOFF
	+FLOAT
	+BOUNCES
	+TRANSLUCENT

	// MBF21 Flags (MBF21 feature set only)
	+LOGRAV
	+HIGHERMPROB

	States
	{
		Spawn:
			SP96 AB 10 A_Look
			Loop
		See:
			SP96 AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			SP96 EF 8 A_FaceTarget
			SP96 G 6 A_TroopAttack
			Goto See
		Pain:
			SP96 H 2
			SP96 H 2 A_Pain
			Goto See
		Death:
			SP96 I 8
			SP96 J 8 A_Scream
			SP96 K 6
			SP96 L 6 A_Fall
			SP96 M 6
			SP96 N -1
			Stop
		Raise:
			SP96 ML 8
			SP96 KJI 6
			Goto See
	}
}

thing 248 : thing 12 "Snake imp"
{
	//$category Monsters/Mid-tier
	ednum 6097

	health 200
	speed 10
	mass 125

	States
	{
		Spawn:
			SP97 AB 10 A_Look
			Loop
		See:
			SP97 AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			SP97 E 8 A_FaceTarget
			SP97 F 8 {
				A_FaceTarget
				A_MonsterProjectile(32,18.0)
				A_MonsterProjectile(32,-18.0)
			}
			SP97 G 6 A_TroopAttack
			Goto See
		Pain:
			SP97 H 2
			SP97 H 2 A_Pain
			Goto See
		Death:
			SP97 I 8
			SP97 J 8 A_Scream
			SP97 K 6
			SP97 L 6 A_Fall
			SP97 M -1
			Stop
		XDeath:
			SP97 N 5
			SP97 O 5 A_XScream
			SP97 P 5
			SP97 Q 5 A_Fall
			SP97 RST 5
			SP97 U -1
			Stop
		Raise:
			SP97 ML 8
			SP97 KJI 6
			Goto See
	}
}