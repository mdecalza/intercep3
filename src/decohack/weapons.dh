/*****************************************************************************
 * DECOHack Patch File
 * Weapons
 * 2024 Matthew DeCalzadilla
 ****************************************************************************/

#include <dsdhacked>

#include "projectiles.dh"

// Rename pickup in editor
thing 78 "Incendiary Shotgun"
{
	health 1001

	States
	{
		Spawn:
			SGN2 A -1
			Stop
	}
}

// Rename pickup in editor
thing 79 "Super Slug Shotgun"
{
	health 1001

	States
	{
		Spawn:
			SHOT A -1
			Stop
	}
}

thing 78 swap with 79

weapon 0 "Fists"
{
	States
	{
		Ready:
			PUNG H 1 A_WeaponReady
			Loop
		Deselect:
			PUNG H 1 A_Lower
			Loop
		Select:
			PUNG H 1 A_Raise
			Loop
		Fire:
			PUNG B 1 {
				A_WeaponJump(RightFistFire, 112)
				A_WeaponSound("SKESWG")
				A_WeaponMeleeAttack(0, 10, 10.0, "SKESWG", 72.0)
			}
			PUNG D 4 A_WeaponMeleeAttack(3, 10, 10.0, "PUNCH", 72.0)
			PUNG C 2
			PUNG C 0 A_RefireTo(FireHold)
			Goto Ready
		RightFistFire:
			PUNG E 1 {
				A_WeaponSound("SKESWG")
				A_WeaponMeleeAttack(0, 10, 10.0, "SKESWG", 72.0)
			}	
			PUNG G 4 A_WeaponMeleeAttack(3, 10, 10.0, "PUNCH", 72.0)
			PUNG F 2
			PUNG F 0 A_RefireTo(FireHold)
			Goto Ready
		FireHold:
			PUNG H 1
			PUNG H 1 A_RefireTo(FireHold)
			Goto Ready
	}
}

weapon 1 "Model 1887"
{
	ammotype 1

	States
	{
		Fire:
			PISG C 3 Bright {
				A_ConsumeAmmo
				A_WeaponBulletAttack(3.75, 3.75, 7)
				A_WeaponSound("FRE022")
			}
			PISG B 4 Bright
			PISG A 6
			PISG A 3 A_ReFire
			PISG D 3
			PISG E 4
			PISG F 6
			PISG G 6 A_WeaponSound("FRE023")
			PISG F 4
			PISG ED 3
			Goto Ready
	}
}

weapon 2 "Super slugger"
{
	// MBF21 fields
	ammopershot 2

	States
	{
		Fire:
			SHTG F 3 Bright {
				A_ConsumeAmmo
				A_WeaponBulletAttack(0.0, 0.0, 3, 80, 2)
				A_WeaponSound("FRE021")
			}
			SHTG A 4
			SHTG B 3
			SHTG C 4
			SHTG D 8
			SHTG E 10
			SHTG D 8
			SHTG C 3
			SHTG B 2
			SHTG A 5 A_ReFire
			Goto Ready
	}
}

weapon 3 "Minigun"
{
	// MBF21 fields
	ammopershot 2

	States
	{
		Fire:
			CHGG A 1 {
				A_ConsumeAmmo
				A_WeaponBulletAttack(5.0, 5.0, 2, 10)
				A_WeaponSound("FRE030")
				A_GunFlashTo(Flash)
			}
			CHGG A 1 A_WeaponBulletAttack(5.0, 5.0, 2, 10)
			CHGG B 1 A_WeaponSound("FRE030")
			CHGG B 1
			CHGG B 1 A_ReFire
			CHGG A 1 A_WeaponSound("FRE029")
			CHGG C 1
			CHGG A 2
			CHGG C 3
			CHGG A 5
			CHGG C 8
			CHGG A 13
			CHGG C 21
			Goto Ready
		Flash:
			CHGF A 2 Bright
			CHGF B 2 Bright
			Stop
	}
}

weapon 4 "Missile launcher"
{
	States
	{
		Flash:
			MISF A 0 Bright
			Stop
	}
}

weapon 5 "Unlocked plasma gun"
{
}

weapon 6 "BFG9000.1"
{
	// MBF21 fields
	ammopershot 100
}

weapon 7 "Model 64 chainsaw"
{
	States
	{
		Ready:
			SAWG C 1 {
				A_WeaponSound("SAWIDL")
				A_WeaponReady
			}
			SAWG D 1 A_WeaponReady
			Loop
		Fire:
			SAWG A 1 A_WeaponMeleeAttack(2, 12, 1.8, "SAWHIT", 73.0)
			SAWG B 1 A_WeaponSound("SAWFUL")
			SAWG B 0 A_ReFire
			Goto Ready
	}
}

weapon 8 "Incendiary shotgun"
{
	// MBF21 fields
	ammopershot 5

	States
	{
		Fire:
			SHT2 B 4 {
				A_ConsumeAmmo
				A_WeaponProjectile(151)
				A_WeaponProjectile(151, 10.0)
				A_WeaponProjectile(151, -10.0)
				A_WeaponProjectile(151, 0.0, 2.5)
				A_WeaponProjectile(151, 0.0, -2.5)
				A_WeaponSound("FRE018")
			}
			SHT2 B 6
			SHT2 C 8
			SHT2 D 8
			SHT2 C 8 A_OpenShotgun2
			SHT2 B 8 A_LoadShotgun2
			SHT2 A 7 A_ReFire
			Goto Ready
		Flash:
			SHT2 E 7 Bright
			SHT2 E 4 Bright
			Stop
	}
}